Olive {
  Program               =  Block
  Block                 =  Statement+
  Statement             =  Ids "=" Exps                                                                       -- mutablebinding
                        |  VarExps ":=" Exps                                                                  -- immutablebinding
                        |  FunctionDecl                                                                       -- functiondeclaration
                        |  "return" Exp?                                                                      -- return
                        |  "while" Exp Suite                                                                  -- while
                        |  "for" id "in" Exp Suite                                                            -- for
                        |  "if" Exp Suite
                           ("else if" Exp Suite)*
                           ("else" Suite)?                                                                    -- if
                        |  Exp                                                                                -- simple
  Suite                 =  indent Block dedent
  Exp                   =  Exp "or" Exp1                                                                      -- or
                        |  Exp "and" Exp1                                                                     -- and
                        |  Exp1
  Exp1                  =  Exp2 relop Exp2                                                                    -- binary
                        |  Exp2
  Exp2                  =  Exp2 addop Exp3                                                                    -- binary
                        |  Exp3
  Exp3                  =  Exp3 mulop Exp4                                                                    -- binary
                        |  Exp4
  Exp4                  =  Exp4 "^" Exp5                                                                      -- binary
                        |  Exp5
  Exp5                  =  prefixop Exp6                                                                      -- unary
                        |  Exp6
  Exp6                  =  Matrix                                                                             -- matrix
                        |  FunctionCall                                                                               -- functioncall
                        |  Dictionary                                                                         -- dictionary
                        |  Set                                                                                -- set
                        |  Key
                        |  "(" Exp ")"                                                                        -- parens
                        |  Tuple
                        |  Range

  Range                 =  ("(" | "[") RangeType ":" RangeType ":" RangeType ("]" | ")")
  Tuple                 =  "(" NonemptyListOf<Exp5, ","> ")"
  Matrix                =  "[" ListOf<Exp5, ","> "]"
  Set                   =  "{" NonemptyListOf<Exp5, ","> "}"
  Dictionary            =  "{" ListOf<KeyValuePair, ","> "}"
  KeyValuePair          =  Key ":" Exp

  RangeType             =  VarExp
                        |  Exp

  Key                   =  boollit
                        |  intlit
                        |  floatlit
                        |  stringlit
                        |  nonelit
                        |  StringInterpolation
                        |  VarExp

  Exps                  =  NonemptyListOf<Exp, ",">
  Ids                   =  NonemptyListOf<id, ",">
  VarExp                =  VarExp "[" Exp "]"                                                                 -- subscripted
                        |  id                                                                                 -- simple
  VarExps               =  NonemptyListOf<VarExp, ",">

  StringInterpolation   = "`" ((~"`" ~"$" any) | Interpolation)* "`"
  Interpolation         = "${" Exp "}"

  types                 =  "int"
                        |  "float"
                        |  "string"
                        |  "bool"
                        |  "None"
                        |  "tuple"
                        |  "matrix"
                        |  "dictionary"
                        |  "set"
                        |  "range"

  basicAnnTypes         =  "None"
                        |  "string"
                        |  "int"
                        |  "bool"
                        |  "range"
                        |  "float"

  FunctionDecl          =  TypeAnn id "(" ListOf<id, ","> ") =" Suite
  TypeAnn               =  id ":" ("_" | ParamAnn) "->" ("_" | Annotation)
  Annotation            =  "matrix<" (Annotation | basicAnnTypes) ">"                                         -- matrix
                        |  "dictionary<" (Annotation | basicAnnTypes) "," (Annotation | basicAnnTypes) ">"    -- dictionary
                        |  "tuple<" NonemptyListOf<(Annotation | basicAnnTypes), ","> ">"                     -- tuple
                        |  "list<" (Annotation | basicAnnTypes) ">"                                           -- list
                        |  "set<" (Annotation | basicAnnTypes) ">"                                            -- set
                        |  "(" ("_" | ParamAnn) "->" ( "_" | Annotation) ")"                                  -- function
                        |  basicAnnTypes                                                                      -- simple
  ParamAnn              =  ListOf<Annotation, ",">
  FunctionCall          =  id "(" ListOf <Arg, ","> ")"
  Arg                   =  Exp
                        |  id

  id                    =  ~keyword letter idrest*
  idrest                =  "_" | alnum
  keyword               = ("true" | "false" | "if"
                        | "else" | "while" | "for" | "in" | "return"
                        | "None" | "not" | "break" | "pass" |  types) ~idrest

  assignop              =  ":=" | "="
  addop                 =  "+" | "-"
  relop                 =  "<=" | "<" | "==" | "!=" | ">=" | ">"
  mulop                 =  "*" | "/%" | "/" | "%"
  prefixop              =  "-" | "not"
  indent                =  "⇨"
  dedent                =  "⇦"

  stringlit             = "\'" ("\\\"" | (~"\'" any))* "\'"
  boollit               = "true" | "false"
  intlit                = ~floatlit digit+
  floatlit              = digit+ "." digit+
  nonelit               = "None"

  newline               =  "\n"+
  space                 += comment
  comment               =  "|" (~"\n" any)* "\n"
}
