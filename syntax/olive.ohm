Olive {
  Program     =  Block
  Block       =  Statement+
  Statement   =  Ids "=" Exps                     -- constdecl
              |  VarExps ":=" Exps                -- varassign
              |  (Annotation)? Function           -- functiondeclaration
              |  "return" Exp?                    -- return
              |  "while" Exp Suite                -- while
              |  "for" id "in" Exp Suite          -- for
              |  "if" Exp Suite
                 ("else if" Exp Suite)*
                 ("else" Suite)?                  -- if
              |  Exp                              -- scripting
  Suite       =  indent Block dedent
  Exp         =  Exp "or" Exp1                    -- or
              |  Exp "and" Exp1                   -- and
              |  Exp1
  Exp1        =  Exp2 relop Exp2                  -- binary
              |  Exp2
  Exp2        =  Exp2 addop Exp3                  -- binary
              |  Exp3
  Exp3        =  Exp3 mulop Exp4                  -- binary
              |  Exp4
  Exp4        =  prefixop Exp5                    -- unary
              |  Exp5
  Exp5        =  Matrix                           -- matrix
              |  Call                             -- functioncall
              |  Dictionary                       -- dictionary
              |  Key
              |  "(" Exp ")"                      -- parens
              |  Tuple

  Tuple        =  "(" NonemptyListOf<Exp4, ","> ")"
  Matrix       =  "[" ListOf<Exp4, ","> "]"
  Dictionary   =  "{" ListOf<KeyValuePair, ","> "}"
  KeyValuePair =  Key ":" Exp

  Key         =  boollit
              |  intlit
              |  floatlit
              |  stringlit
              |  nonelit
              |  StringInterpolation
              |  VarExp
  Exps        =  NonemptyListOf<Exp, ",">
  Ids         =  NonemptyListOf<id, ",">
  VarExp      =  VarExp "[" Exp "]"               -- subscripted
              |  id                               -- simple
  VarExps     =  NonemptyListOf<VarExp, ",">

  StringInterpolation = "`" ((~"`" ~"$" any) | Interpolation)* "`"
  Interpolation = "${" Exp "}"

  Type        =  "int"
              |  "float"
              |  "string"
              |  "bool"
              |  "None"
              |  "tuple"
              |  "matrix"
              |  "dictionary"
              |  "set"

  Annotation  =  id ":" (Type ("," Type)*)? "->" Type?

  Function    =  id "(" Parameters? ")" "=" Suite -- regularfuctions
              | "lambda" Unnamed ":" Exp          -- anonymousfunctions

  Parameters  =  Named                            -- onlynamedparams
              |  Unnamed "," Named                -- both
              |  Unnamed                          -- onlyunnamedparams

  Named       =  id "=" Exp ("," id "=" Exp)*     -- named
  Unnamed     =  id ("," id ~ "=")*               -- unnamed

  Call        =  id "(" Arguments? ")"             -- functioncall

  Arguments   =  Named                            -- onlynamedargs
              |  UnnamedArgs "," Named            -- both
              |  UnnamedArgs                      -- onlyunnamedargs

  UnnamedArgs =  Arg ("," Arg ~ "=")*             -- unnamedargs

  Arg         = Exp
              | id

  id          =  ~keyword letter idrest*
  idrest      =  "_" | alnum
  keyword     = ("true" | "false" | "if"
              | "else" | "while" | "for" | "in" | "return"
              | "None" | "not" | "break" | "pass") ~idrest

  assignop    =  ":=" | "="
  addop       =  "+" | "-"
  relop       =  "<=" | "<" | "==" | "!=" | ">=" | ">"
  mulop       =  "*" | "/%" | "/" | "%"
  prefixop    =  "-" | "not"
  indent      =  "⇨"
  dedent      =  "⇦"

  stringlit   = "\'" ("\\\"" | (~"\'" any))* "\'"
  boollit     = "true" | "false"
  intlit      = ~floatlit digit+
  floatlit    = digit+ "." digit+
  nonelit     = "None"

  newline     =  "\n"+
  space       += comment
  comment     =  "|" (~"\n" any)* "\n"
}
